buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://artifacts.fizzpod.com/public/mvn/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.fizzpod:gradle-java-opinion:0.14.28'
        classpath 'io.freefair.gradle:lombok-plugin:8.14'
        classpath 'io.freefair.gradle:aspectj-plugin:8.14'
    }
}

apply plugin: 'com.fizzpod.pater-build'
apply plugin: 'com.fizzpod.github-release'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'io.freefair.lombok'
apply plugin: 'io.freefair.aspectj.post-compile-weaving'
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
compileJava {
    options.release = 17
}

compileGroovy {
    options.release = 17
}

jacoco {
    toolVersion = "0.8.8"
}

dependencies {
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.788')
    
    implementation 'org.apache.groovy:groovy-all:4.0.28'

    implementation 'com.amazonaws:aws-lambda-java-core:1.3.0'
    implementation 'com.amazonaws:aws-lambda-java-events:3.16.1'

    implementation 'com.github.tomakehurst:wiremock-jre8:3.0.1'

    implementation 'org.apache.commons:commons-lang3:3.18.0'

    implementation 'org.aspectj:aspectjrt:1.9.24'

    aspect 'software.amazon.lambda:powertools-logging:1.20.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testImplementation 'org.mockito:mockito-core:5.19.0'
    testImplementation 'com.amazonaws:aws-lambda-java-tests:1.1.2'

    constraints {
        implementation('org.xmlunit:xmlunit-core') {
            because 'version 2.9.1 imported indirectly as a dependency has a vulnerability'
            version {
                require '2.10.0'
            }
        }

        implementation('org.eclipse.jetty:jetty-bom') {
            because 'version 11.0.15 imported indirectly as a dependency has a vulnerability'
            version {
                require '11.0.24'
            }
        }

        implementation('com.jayway.jsonpath:json-path') {
            because 'version 2.8.0 imported indirectly as a dependency has a vulnerability'
            version {
                require '2.9.0'
            }
        }

        implementation('org.wiremock:wiremock') {
            because 'version 3.0.1 imported indirectly as a dependency has a vulnerability'
            version {
                require '3.0.4'
            }
        }

        implementation('commons-io:commons-io') {
            because 'version 2.14.0 imported indirectly as a dependency has a vulnerability'
            version {
                require '2.14.0'
            }
        }
 
        implementation('net.minidev:json-smart') {
            //TODO change this to 2.5.2 when it is released
            because 'version 2.5.0 imported as a dependency has a vulnerability'
            version {
                require '2.5.2'
            }
        }

        implementation("commons-fileupload:commons-fileupload") {
            because 'version 1.5.0 imported as a dependency has a vulnerability'
            version {
                require '1.6.0'
            }
        }

        implementation("rg.apache.commons:commons-lang3") {
            because 'version 3.14.0 imported as a dependency has a vulnerability'
            version {
                require '3.18.0'
            }
        }
        
    }
}

test {
    useJUnitPlatform()
}

githubRelease {
    token project.hasProperty("github_release_token")? github_release_token: ""
    tagName 'release-' + project.version
    releaseName 'Release ' + project.version
    owner "boxheed" 
    targetCommitish "main" 
    generateReleaseNotes true 
    draft true 
    dryRun false
    releaseAssets { ->
        jar.outputs.files
    }
}
